
&НаКлиенте
Процедура Печать1(Команда)
	ТабДок = Печать1НаСервере();
	ТабДок.Показать();
КонецПроцедуры

&НаСервере
Функция Печать1НаСервере()
	ТабДок = Новый ТабличныйДокумент();
	
	Макет = Документы.СчетНаОплату.ПолучитьМакет("Макет");
	
	ОблШапка = Макет.ПолучитьОбласть("Заголовок");
	ОблШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОблСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОблПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ОблШапка.Параметры.Номер = Объект.Номер;
	ОблШапка.Параметры.Дата = Формат(Объект.Дата, "ДЛФ=DD;");
	ОблШапка.Параметры.Контрагент = Объект.Контрагент;
	ОблШапка.Параметры.Договор = Объект.Договор;
	
	ТабДок.Вывести(ОблШапка);
	ТабДок.Вывести(ОблШапкаТаблицы);
	
	Для Каждого Стр Из Объект.Товары Цикл
		ОблСтрокаТаблицы.Параметры.Заполнить(Стр);
		ТабДок.Вывести(ОблСтрокаТаблицы);	
	КонецЦикла;
	
	ОблПодвал.Параметры.СуммаПрописью = ЧислоПрописью(Объект.Товары.Итог("Сумма"),,"рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2");
	
	ТабДок.Вывести(ОблПодвал);
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьГруппировки = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;	
	
	Возврат ТабДок;	
	
КонецФункции

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	Товар = Элементы.Товары.ТекущиеДанные.Номенклатура;
	Элементы.Товары.ТекущиеДанные.Цена = ПолучитьЦену(Товар);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦену(Товар)
	//Возврат Товар.ЦенаПродажи;	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Номенклатура.ЦенаПродажи
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Товар);
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Следующий() Тогда
		Возврат РезультатЗапроса.ЦенаПродажи;	
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСумму(Стр)
	Стр.Сумма = Стр.Количество * Стр.Цена;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	РассчитатьСумму(Элементы.Товары.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	РассчитатьСумму(Элементы.Товары.ТекущиеДанные);
КонецПроцедуры


&НаСервере
Процедура ДатьСкидкуНаСервере()
	
	Для каждого Стр Из Объект.Товары Цикл
		Стр.Цена = Стр.Цена * 0.9;
		РассчитатьСумму(Стр);
	КонецЦикла; 
	
КонецПроцедуры


&НаКлиенте
Процедура ДатьСкидку(Команда)
	ДатьСкидкуНаСервере();
КонецПроцедуры
 